#
# Copyright 2013-2017 RIKEN
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

# Author: Masatomo Hashimoto <m.hashimoto@riken.jp>


DEFINE input:inference "ont.cpi"

PREFIX fb:      <http://codinuum.com/fb/>
PREFIX ent:     <http://codinuum.com/fact/entity/>
PREFIX rel:     <http://codinuum.com/fact/version/release/>
PREFIX variant: <http://codinuum.com/fact/version/variant/>
PREFIX src:     <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:     <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX f:       <http://codinuum.com/ontologies/2013/05/fortran-entity#>


INSERT {
  ?sp f:provides ?mext .
  ?sp f:includedInModule ?mod .
  ?sp a ?cat .
}
WHERE {

  ?incl a f:PpInclude ;
        f:path ?path ;
        f:inSubprogramPart/f:inModule ?mod .

  ?mod a f:Module ;
       f:name ?mname .

  ?sp a f:ExternalSubprogram ;
      src:inFile/src:location ?path ;
      f:name ?sp_name ;
      f:provides ?ext .

  {
    ?sp a f:SubroutineExternalSubprogram .
    BIND (f:SubroutineModuleSubprogram AS ?cat)
  }
  UNION
  {
    ?sp a f:FunctionExternalSubprogram .
    BIND (f:FunctionModuleSubprogram AS ?cat)
  }

  BIND (IRI(CONCAT("http://codinuum.com/fact/external-name/fortran/",
                   ?mname,
                   "-",
                   ?sp_name)) AS ?mext)

}
